buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = "${mcversion}-${version}"
group = "thecodex6824.thaumicaugmentation" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "ThaumicAugmentation"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2824"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "stable_39"
    makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    
    replace '@APIVERSION@', project.apiversion
    replaceIn 'ThaumicAugmentationAPI.java'
    
    replace '@VERSION@', project.version
    replaceIn 'ThaumicAugmentation.java'
}

repositories {
	maven {
    	name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
}

dependencies {
    deobfCompile "thaumcraft:Thaumcraft:1.12.2:6.1.BETA26"
    
    runtime "baubles:Baubles:1.12:1.5.2"
    runtime "jei:jei_1.12.2:4.15.0.271"
    runtime "thaumic-jei:ThaumicJEI-1.12.2:1.5.5:23"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "apiversion", project.apiversion
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task javadocJar(type: Jar) {
	from 'build/docs/javadoc'
	classifier = 'javadoc'
	include 'thecodex6824/thaumicaugmentation/api/**'
}

task apiJar(type: Jar) {
	from sourceSets.main.output
	from sourceSets.main.java
	classifier = 'api'
	include 'thecodex6824/thaumicaugmentation/api/**'
}

artifacts {
	archives apiJar
	archives javadocJar
}
